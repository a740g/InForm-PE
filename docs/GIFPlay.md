# Animated GIF Player Library

The `GIFPlay` library provides functionality for playing [animated GIF files](https://en.wikipedia.org/wiki/GIF#Animated_GIF). It can be used as a standalone library or integrated with InForm-PE.

## Usage

You can use the library either standalone or with InForm-PE.

### Standalone

To use the `GIFPlay` library in a standalone project, you need to include `GIFPlay.bi` at the top of your code and `GIFPlay.bm` at the bottom.

```vb
OPTION _EXPLICIT

'$INCLUDE:'GIFPlay.bi'

$RESIZE:SMOOTH

CONST GIF_ID = 123

DO
    DIM gifFileName AS STRING: gifFileName = _OPENFILEDIALOG$("Open GIF", , "*.gif|*.GIF|*.Gif", "GIF Files")
    IF LEN(gifFileName) = 0 THEN EXIT DO

    IF GIF_LoadFromFile(GIF_ID, gifFileName) THEN
        DIM surface AS LONG: surface = _NEWIMAGE(GIF_GetWidth(GIF_ID), GIF_GetHeight(GIF_ID), 32)
        SCREEN surface
        _ALLOWFULLSCREEN _SQUAREPIXELS , _SMOOTH

        GIF_Play GIF_ID

        DO
            DIM k AS LONG: k = _KEYHIT

            IF k = _ASC_SPACE THEN
                IF GIF_IsPlaying(GIF_ID) THEN GIF_Pause (GIF_ID) ELSE GIF_Play (GIF_ID)
            END IF

            CLS
            GIF_Draw GIF_ID
            _DISPLAY

            _LIMIT 30
        LOOP UNTIL k = _KEY_ESC

        SCREEN 0
        _FREEIMAGE surface
    END IF
LOOP

END

'$INCLUDE:'GIFPlay.bm'
```

### With InForm-PE

To use the `GIFPlay` library with InForm-PE, your form must contain a PictureBox control that will serve as a container for the GIF animation.

In the external modules section of the `.bas` file generated by InForm-PE, you must include both `GIFPlay.bi` and `GIFPlay.bm`. `GIFPlay.bi` must be included **before** `InForm.ui`, and `GIFPlay.bm` must be included **after** `InForm.ui`.

```vb
': External modules: --------------------------------
'$INCLUDE:'InForm/extensions/GIFPlay.bi'
'$INCLUDE:'InForm/InForm.ui'
'$INCLUDE:'InForm/xp.uitheme'
'$INCLUDE:'gifplaySample.frm'
'$INCLUDE:'InForm/extensions/GIFPlay.bm'
```

## API Reference

### Initialization and Loading

Loads a GIF animation from a file. Returns `_TRUE` if the GIF is loaded successfully, `_FALSE` otherwise.

```vb
FUNCTION GIF_LoadFromFile%% (Id AS LONG, fileName AS STRING)
```

Loads a GIF animation from a string buffer. Returns `_TRUE` if the GIF is loaded successfully, `_FALSE` otherwise.

```vb
FUNCTION GIF_LoadFromMemory%% (Id AS LONG, buffer AS STRING)
```

Frees the resources used by a loaded GIF.

```vb
SUB GIF_Free (Id AS LONG)
```

Checks if a GIF is fully loaded and ready for playback. Returns `_TRUE` if the GIF is loaded, `_FALSE` otherwise.

```vb
FUNCTION GIF_IsLoaded%% (Id AS LONG)
```

### Playback Control

Starts playing a loaded GIF.

```vb
SUB GIF_Play (Id AS LONG)
```

Pauses the playback of a GIF.

```vb
SUB GIF_Pause (Id AS LONG)
```

Stops the playback of a GIF and resets it to the first frame.

```vb
SUB GIF_Stop (Id AS LONG)
```

Sets the looping behavior of a GIF, where < 0 means play once, 0 means loop forever and > 0 means loop `n` times.

```vb
SUB GIF_SetLoop (Id AS LONG, loops AS LONG)
```

### Information and Properties

Returns the width of the GIF in pixels.

```vb
FUNCTION GIF_GetWidth~& (Id AS LONG)
```

Returns the height of the GIF in pixels.

```vb
FUNCTION GIF_GetHeight~& (Id AS LONG)
```

Returns the current frame number.

```vb
FUNCTION GIF_GetFrameNumber~& (Id AS LONG)
```

Returns the total number of frames in the GIF.

```vb
FUNCTION GIF_GetTotalFrames~& (Id AS LONG)
```

Checks if a GIF is currently playing. Returns `_TRUE` if the GIF is playing, `_FALSE` otherwise.

```vb
FUNCTION GIF_IsPlaying%% (Id AS LONG)
```

Returns the total duration of the animation in milliseconds.

```vb
FUNCTION GIF_GetTotalDuration~&& (Id AS LONG)
```

Returns the duration of the current frame in milliseconds.

```vb
FUNCTION GIF_GetFrameDuration~&& (Id AS LONG)
```

Returns the elapsed time of the animation in milliseconds.

```vb
FUNCTION GIF_GetElapsedTime~&& (Id AS LONG)
```

### Drawing and Rendering

Draws the current frame of the GIF to the destination surface @ (0, 0) (stretching the frame if needed). When using InForm-PE, GIF_Draw must be called from within the `__UI_BeforeUpdateDisplay` event. That's where the frames will be updated in your PictureBox control. When used as a standalone library, you should call this function in your main loop to draw the animation.

```vb
SUB GIF_Draw (Id AS LONG)
```

Returns the image handle of the current frame. Use this function if you want to perform custom drawing operations. The library manages the memory for this image.

```vb
FUNCTION GIF_GetFrame& (Id AS LONG)
```

Returns the background color of the GIF. This can be used to clear the drawing surface before rendering a frame.

```vb
FUNCTION GIF_GetBackgroundColor~& (Id AS LONG)
```

Enables or disables the GIF overlay image that is shown when the animation is not playing.

```vb
SUB GIF_EnableOverlay (Id AS LONG, isEnabled AS _BYTE)
```
