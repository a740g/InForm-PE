'-----------------------------------------------------------------------------------------------------------------------
' Time related routines
' Copyright (c) 2025 Samuel Gomes
'-----------------------------------------------------------------------------------------------------------------------

$INCLUDEONCE

'$INCLUDE:'Time.bi'

''' @brief Measures the number of times this function is called per second.
''' @return The number of calls per second.
FUNCTION Time_MeasureHertz~&
    STATIC AS _UNSIGNED LONG counter, finalHz
    STATIC lastMs AS _UNSIGNED _INTEGER64

    DIM currentMs AS _UNSIGNED _INTEGER64: currentMs = Time_GetTicks

    IF currentMs - lastMs >= 1000 THEN
        lastMs = currentMs
        finalHz = counter
        counter = 0
    END IF

    counter = counter + 1

    Time_MeasureHertz = finalHz
END FUNCTION
