'-----------------------------------------------------------------------------------------------------------------------
' String related stuff
' Copyright (c) 2025 Samuel Gomes
'-----------------------------------------------------------------------------------------------------------------------

$INCLUDEONCE

'$INCLUDE:'String.bi'

''' @brief Converts a character to lowercase.
''' @param c The ASCII code of the character to convert.
''' @return The ASCII code of the lowercase character.
FUNCTION Asc_ToLowercase~%% (c AS _UNSIGNED _BYTE)
    Asc_ToLowercase = _IIF(c >= __ASC_UPPER_A _ANDALSO c <= __ASC_UPPER_Z, c + 32, c)
END FUNCTION

''' @brief Converts a character to uppercase.
''' @param c The ASCII code of the character to convert.
''' @return The ASCII code of the uppercase character.
FUNCTION Asc_ToUppercase~%% (c AS _UNSIGNED _BYTE)
    Asc_ToUppercase = _IIF(c >= __ASC_LOWER_A _ANDALSO c <= __ASC_LOWER_Z, c - 32, c)
END FUNCTION

''' @brief Checks if a character is alphanumeric.
''' @param c The ASCII code of the character to check.
''' @return _TRUE if the character is alphanumeric, _FALSE otherwise.
FUNCTION Asc_IsAlphanumeric%% (c AS _UNSIGNED _BYTE)
    Asc_IsAlphanumeric = (c >= __ASC_0 _ANDALSO c <= __ASC_9) _ORELSE (c >= __ASC_UPPER_A _ANDALSO c <= __ASC_UPPER_Z) _ORELSE (c >= __ASC_LOWER_A _ANDALSO c <= __ASC_LOWER_Z)
END FUNCTION

''' @brief Checks if a character is alphabetic.
''' @param c The ASCII code of the character to check.
''' @return _TRUE if the character is alphabetic, _FALSE otherwise.
FUNCTION Asc_IsAlphabetic%% (c AS _UNSIGNED _BYTE)
    Asc_IsAlphabetic = (c >= __ASC_UPPER_A _ANDALSO c <= __ASC_UPPER_Z) _ORELSE (c >= __ASC_LOWER_A _ANDALSO c <= __ASC_LOWER_Z)
END FUNCTION

''' @brief Checks if a character is lowercase.
''' @param c The ASCII code of the character to check.
''' @return _TRUE if the character is lowercase, _FALSE otherwise.
FUNCTION Asc_IsLowercase%% (c AS _UNSIGNED _BYTE)
    Asc_IsLowercase = c >= __ASC_LOWER_A _ANDALSO c <= __ASC_LOWER_Z
END FUNCTION

''' @brief Checks if a character is uppercase.
''' @param c The ASCII code of the character to check.
''' @return _TRUE if the character is uppercase, _FALSE otherwise.
FUNCTION Asc_IsUppercase%% (c AS _UNSIGNED _BYTE)
    Asc_IsUppercase = c >= __ASC_UPPER_A _ANDALSO c <= __ASC_UPPER_Z
END FUNCTION

''' @brief Checks if a character is a decimal digit.
''' @param c The ASCII code of the character to check.
''' @return _TRUE if the character is a decimal digit, _FALSE otherwise.
FUNCTION Asc_IsDecimalDigit%% (c AS _UNSIGNED _BYTE)
    Asc_IsDecimalDigit = c >= __ASC_0 _ANDALSO c <= __ASC_9
END FUNCTION

''' @brief Checks if a character is a hexadecimal digit.
''' @param c The ASCII code of the character to check.
''' @return _TRUE if the character is a hexadecimal digit, _FALSE otherwise.
FUNCTION Asc_IsHexadecimalDigit%% (c AS _UNSIGNED _BYTE)
    Asc_IsHexadecimalDigit = (c >= __ASC_0 _ANDALSO c <= __ASC_9) _ORELSE (c >= __ASC_UPPER_A _ANDALSO c <= __ASC_UPPER_F) _ORELSE (c >= __ASC_LOWER_A _ANDALSO c <= __ASC_LOWER_F)
END FUNCTION

''' @brief Checks if a character is an octal digit.
''' @param c The ASCII code of the character to check.
''' @return _TRUE if the character is an octal digit, _FALSE otherwise.
FUNCTION Asc_IsOctalDigit%% (c AS _UNSIGNED _BYTE)
    Asc_IsOctalDigit = c >= __ASC_0 _ANDALSO c <= __ASC_7
END FUNCTION

''' @brief Checks if a character is a binary digit.
''' @param c The ASCII code of the character to check.
''' @return _TRUE if the character is a binary digit, _FALSE otherwise.
FUNCTION Asc_IsBinaryDigit%% (c AS _UNSIGNED _BYTE)
    Asc_IsBinaryDigit = c = __ASC_0 _ORELSE c = __ASC_1
END FUNCTION

''' @brief Checks if a character is a control character.
''' @param c The ASCII code of the character to check.
''' @return _TRUE if the character is a control character, _FALSE otherwise.
FUNCTION Asc_IsControlCharacter%% (c AS _UNSIGNED _BYTE)
    Asc_IsControlCharacter = c < _ASC_SPACE _ORELSE c = _ASC_DEL
END FUNCTION

''' @brief Checks if a character is whitespace.
''' @param c The ASCII code of the character to check.
''' @return _TRUE if the character is whitespace, _FALSE otherwise.
FUNCTION Asc_IsWhitespace%% (c AS _UNSIGNED _BYTE)
    Asc_IsWhitespace = c = _ASC_SPACE _ORELSE c = _ASC_HT _ORELSE c = _ASC_LF _ORELSE c = _ASC_VT _ORELSE c = _ASC_FF _ORELSE c = _ASC_CR
END FUNCTION

''' @brief Checks if a character is blank (space or horizontal tab).
''' @param c The ASCII code of the character to check.
''' @return _TRUE if the character is blank, _FALSE otherwise.
FUNCTION Asc_IsBlank%% (c AS _UNSIGNED _BYTE)
    Asc_IsBlank = c = _ASC_SPACE _ORELSE c = _ASC_HT
END FUNCTION

''' @brief Checks if a character is punctuation.
''' @param c The ASCII code of the character to check.
''' @return _TRUE if the character is punctuation, _FALSE otherwise.
FUNCTION Asc_IsPunctuation%% (c AS _UNSIGNED _BYTE)
    Asc_IsPunctuation = (c >= _ASC_EXCLAMATION _ANDALSO c <= _ASC_FORWARDSLASH) _ORELSE (c >= _ASC_COLON _ANDALSO c <= _ASC_ATSIGN) _ORELSE (c >= _ASC_LEFTSQUAREBRACKET _ANDALSO c <= _ASC_GRAVE) _ORELSE (c >= _ASC_LEFTCURLYBRACKET _ANDALSO c <= _ASC_TILDE)
END FUNCTION

''' @brief Converts a C-style string (null-terminated) to a BASIC string.
''' @param s The C-style string to convert.
''' @return The converted BASIC string.
FUNCTION String_ToBStr$ (s AS STRING)
    DIM nulPos AS _UNSIGNED LONG: nulPos = INSTR(s, _CHR_NUL)
    String_ToBStr = _IIF(nulPos, LEFT$(s, nulPos - 1), s)
END FUNCTION

''' @brief Converts a BASIC string to a C-style string (null-terminated).
''' @param s The BASIC string to convert.
''' @return The converted C-style string.
FUNCTION String_ToCStr$ (s AS STRING)
    String_ToCStr = s + _CHR_NUL
END FUNCTION

''' @brief Formats a string using a format string and a STRING argument.
''' @param fmtStr The format string.
''' @param s The STRING argument.
''' @return The formatted string.
FUNCTION String_FormatString$ (fmtStr AS STRING, s AS STRING)
    DECLARE LIBRARY
        SUB __String_FormatString ALIAS "snprintf" (buf AS STRING, BYVAL bufSize AS _UNSIGNED _OFFSET, fmt AS STRING, s AS STRING)
    END DECLARE

    DIM buffer AS STRING: buffer = STRING$(LEN(fmtStr) + LEN(s) + 16, _ASC_NUL)
    __String_FormatString buffer, LEN(buffer), String_ToCStr(fmtStr), String_ToCStr(s)

    String_FormatString = String_ToBStr(buffer)
END FUNCTION

''' @brief Formats a string using a format string and a LONG argument.
''' @param fmtStr The format string.
''' @param n The LONG argument.
''' @return The formatted string.
FUNCTION String_FormatLong$ (fmtStr AS STRING, n AS LONG)
    DECLARE LIBRARY
        SUB __String_FormatLong ALIAS "snprintf" (buf AS STRING, BYVAL bufSize AS _UNSIGNED _OFFSET, fmt AS STRING, BYVAL n AS LONG)
    END DECLARE

    DIM buffer AS STRING: buffer = STRING$(LEN(fmtStr) + 64, _ASC_NUL)
    __String_FormatLong buffer, LEN(buffer), String_ToCStr(fmtStr), n

    String_FormatLong = String_ToBStr(buffer)
END FUNCTION

''' @brief Formats a string using a format string and a _INTEGER64 argument.
''' @param fmtStr The format string.
''' @param n The _INTEGER64 argument.
''' @return The formatted string.
FUNCTION String_FormatInteger64$ (fmtStr AS STRING, n AS _INTEGER64)
    DECLARE LIBRARY
        SUB __String_FormatInteger64 ALIAS "snprintf" (buf AS STRING, BYVAL bufSize AS _UNSIGNED _OFFSET, fmt AS STRING, BYVAL n AS _INTEGER64)
    END DECLARE

    DIM buffer AS STRING: buffer = STRING$(LEN(fmtStr) + 128, _ASC_NUL)
    __String_FormatInteger64 buffer, LEN(buffer), String_ToCStr(fmtStr), n

    String_FormatInteger64 = String_ToBStr(buffer)
END FUNCTION

''' @brief Formats a string using a format string and a SINGLE argument.
''' @param fmtStr The format string.
''' @param f The SINGLE argument.
''' @return The formatted string.
FUNCTION String_FormatSingle$ (fmtStr AS STRING, f AS SINGLE)
    DECLARE LIBRARY
        SUB __String_FormatSingle ALIAS "snprintf" (buf AS STRING, BYVAL bufSize AS _UNSIGNED _OFFSET, fmt AS STRING, BYVAL f AS SINGLE)
    END DECLARE

    DIM buffer AS STRING: buffer = STRING$(LEN(fmtStr) + 256, _ASC_NUL)
    __String_FormatSingle buffer, LEN(buffer), String_ToCStr(fmtStr), f

    String_FormatSingle = String_ToBStr(buffer)
END FUNCTION

''' @brief Formats a string using a format string and a DOUBLE argument.
''' @param fmtStr The format string.
''' @param d The DOUBLE argument.
''' @return The formatted string.
FUNCTION String_FormatDouble$ (fmtStr AS STRING, d AS DOUBLE)
    DECLARE LIBRARY
        SUB __String_FormatDouble ALIAS "snprintf" (buf AS STRING, BYVAL bufSize AS _UNSIGNED _OFFSET, fmt AS STRING, BYVAL d AS DOUBLE)
    END DECLARE

    DIM buffer AS STRING: buffer = STRING$(LEN(fmtStr) + 512, _ASC_NUL)
    __String_FormatDouble buffer, LEN(buffer), String_ToCStr(fmtStr), d

    String_FormatDouble = String_ToBStr(buffer)
END FUNCTION
